plugins {
  id 'application'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.springframework.boot' version '3.5.3'
  id 'com.github.ben-manes.versions' version '0.52.0'
  id 'org.sonarqube' version '6.2.0.5505'
  id 'uk.gov.hmcts.java' version '0.12.67'
  id 'io.freefair.lombok' version '8.14.2'
}

application {
  mainClass = 'uk.gov.hmcts.reform.bsp.Application'
  group = 'uk.gov.hmcts.reform'
  version = '0.0.1'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

def configureSourceSet(String name) {
  sourceSets.create(name) { sourceSet ->
    sourceSet.java {
      compileClasspath += sourceSets.main.output + sourceSets.test.output
      runtimeClasspath += sourceSets.main.output + sourceSets.test.output
      srcDir "src/${name}/java"
    }
    sourceSet.resources.srcDir "src/${name}/resources"
  }
}

['integrationTest'].each { configureSourceSet(it) }

configurations {
  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

tasks.withType(JavaCompile).configureEach {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()
  failFast = true

  testLogging {
    exceptionFormat = 'full'
    jvmArgs '-Djava.security.manager=allow'
  }
}

tasks.register('integration', Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.getRequired().set(true)
    csv.getRequired().set(false)
    html.getRequired().set(true)
  }
}

tasks.sonarqube.dependsOn(jacocoTestReport)
tasks.check.dependsOn(integration)

sonarqube {
  properties {
    property "sonar.projectName", "Reform :: bsp-cron-trigger"
    property "sonar.projectKey", "uk.gov.hmcts.reform:bsp-cron-trigger"
  }
}

dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection ->
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

dependencyCheck {
  suppressionFile = 'config/owasp/suppressions.xml'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url = uri('https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1')
  }
}

dependencies {
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.17.0'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '4.3.0'
  implementation group: 'com.slack.api', name: 'slack-api-client', version: '1.45.4'

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: '6.1.9'

  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  testImplementation group: 'com.github.stefanbirkner', name: 'system-lambda', version: '1.2.1'
  testImplementation group: 'io.github.hakky54', name: 'logcaptor', version: '2.12.1'
  testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '5.5.6'
}

bootJar {
  archiveFileName = "bsp-cron-trigger.jar"

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
rootProject.tasks.named("processIntegrationTestResources") {
  duplicatesStrategy = 'include'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

dependencyManagement {
  dependencies {
    // CVE-2025-48976
    dependency group: 'commons-fileupload', name: 'commons-fileupload', version: '1.6.0'
  }
}
